plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'signing'
    id 'app.opendocument.conanandroidgradleplugin'
}

group = rootProject.group

["armv7", "armv8", "x86", "x86_64"].each { arch ->
    tasks.named("conanInstall-" + arch) {
        profile.set("android-21-" + arch)
    }
}

android {
    namespace "app.opendocument.android.pdf2htmlex"
    compileSdk 35

    defaultConfig {
        minSdk 21
        targetSdk 35

        archivesBaseName = rootProject.name

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

        externalNativeBuild {
            cmake {
                arguments(
                        // ðŸ”¥ Chuáº©n NDK 26: STL
                        "-DCMAKE_ANDROID_STL_TYPE=c++_shared",

                        // ðŸ”¥ Conan toolchain
                        "-DCMAKE_TOOLCHAIN_FILE=build/conan/android_toolchain.cmake",

                        "-DCMAKE_BUILD_TYPE=RelWithDebInfo"
                )
            }
        }

        buildConfigField("String", "VERSION_NAME", "\"${rootProject.version}\"")
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }

    buildFeatures {
        prefab true
        buildConfig true
    }

    externalNativeBuild {
        cmake {
            path 'CMakeLists.txt'
            version '3.22.1'
        }
    }

    ndkVersion '26.3.11579264'

    sourceSets.main.assets.srcDirs += "build/conan/armv8/assets"
    sourceSets.androidTest.assets.srcDirs = [
            getProject().parent.getLayout().getProjectDirectory().dir("test/androidTestAssets")
    ]
}

// Ensure Conan assets are ready
afterEvaluate {
    tasks.named("generateDebugAssets").configure {
        dependsOn(tasks.named("conanInstall-armv8"))
    }
    tasks.named("generateReleaseAssets").configure {
        dependsOn(tasks.named("conanInstall-armv8"))
    }
}

dependencies {
    implementation 'androidx.annotation:annotation:1.8.2'
    implementation 'com.getkeepsafe.relinker:relinker:1.4.5'
    implementation 'com.viliussutkus89:fontconfig-android-translator:1.0.2'
    implementation 'com.viliussutkus89:assetextractor-android:1.3.3'

    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
}

publishing {
    publications {
        release(MavenPublication) {
            afterEvaluate {
                from components.release
            }
            artifactId rootProject.name
            version = rootProject.version

            pom {
                name = rootProject.name
                description = 'pdf2htmlEX library port for Android - Convert PDF to HTML without losing text or format'
                url = 'https://github.com/opendocument-app/pdf2htmlEX-Android'
                packaging = 'aar'
                inceptionYear = '2019'

                developers {
                    developer {
                        id = "ViliusSutkus89"
                        name = "Vilius Sutkus"
                        email = "Vilius@ViliusSutkus89.com"
                    }
                }

                scm {
                    url = 'https://github.com/opendocument-app/pdf2htmlEX-Android'
                    connection = 'https://github.com/opendocument-app/pdf2htmlEX-Android.git'
                }

                licenses {
                    license {
                        name = 'GPLv3'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.html'
                        distribution = 'repo'
                    }
                }
            }
        }
    }
}

if (System.getenv('SIGNING_KEY')) {
    signing {
        required { true }
        useInMemoryPgpKeys(System.getenv('SIGNING_KEY'), System.getenv('SIGNING_PASS'))
        sign publishing.publications.release
    }
}

// Fix Gradle double clean crash
tasks.named("clean") {
    def dotCxxDir = getLayout().getProjectDirectory().dir(".cxx")
    doFirst {
        delete dotCxxDir
    }
}
